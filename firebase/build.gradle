apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply from: '../buildSrc/pmd.gradle'
apply from: '../buildSrc/checkstyle.gradle'
apply from: '../buildSrc/findbugs.gradle'

android {


    compileSdkVersion Versions.compile_sdk
    defaultConfig {
        minSdkVersion Versions.min_sdk
    }


    buildTypes {
        firebase {

            resValue "string", "default_web_client_id", SystemEnv.default_web_client_id ?: ""
            resValue "string", "firebase_database_url", SystemEnv.firebase_database_url ?: ""
            resValue "string", "gcm_defaultSenderId", SystemEnv.gcm_defaultSenderId ?: ""
            resValue "string", "google_api_key", SystemEnv.google_api_key ?: ""
            resValue "string", "google_app_id", SystemEnv.google_app_id ?: ""
            resValue "string", "google_crash_reporting_api_key", SystemEnv.google_crash_reporting_api_key ?: ""
            resValue "string", "project_id", SystemEnv.project_id ?: ""
            // plugin io.fabric should generate this build_id but somehow it just won't work here.
            // In the log it says "Crashlytics was applied to an android-library project.
            // Android-library support is currently an incubating feature. Contact support@fabric.io with any issues."
            // So maybe we can do that later.
            // Since there's no source code for me to trace, I'll do it manually.
            // For more details, use ./gradlew help --task fabricGenerateResourcesDebug
            resValue "string", "com.crashlytics.android.build_id", UUID.randomUUID().toString()
        }

        firebase_no_op {
        }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${Versions.kotlin}"

    implementation "com.google.firebase:firebase-core:${Versions.firebase}"
    implementation "com.google.firebase:firebase-config:${Versions.firebase}"
    api "com.google.firebase:firebase-messaging:${Versions.fcm}"
    implementation "com.crashlytics.sdk.android:crashlytics:${Versions.crashlytics}"

}

android.variantFilter { variant ->
    if (variant.buildType.name.endsWith('release') || variant.buildType.name.endsWith('debug')) {
        variant.setIgnore(true);
    }
}

afterEvaluate {
    check.dependsOn 'findbugs', 'pmd', 'checkstyle'
}
repositories {
    mavenCentral()
}
